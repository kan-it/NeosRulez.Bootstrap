prototype(Neos.NodeTypes.AssetList:AssetList) < prototype(Neos.Neos:ContentComponent) {
    assets = ${node.properties.assets}

    renderer = afx`
        <NeosRulez.Bootstrap:Component.AbstractContent>
            <code @if.render={!props.assets} >
                {I18n.translate('Neos.NodeTypes.AssetList:NodeTypes.AssetList:content.emptyAssetList')}
            </code>
            <div @if.render={props.assets} class="list-group">
                <Neos.Fusion:Loop items={props.assets} itemName="asset">
                    <Neos.NodeTypes.AssetList:AssetList.LinkItem asset={asset} />
                </Neos.Fusion:Loop>
            </div>
        </NeosRulez.Bootstrap:Component.AbstractContent>
    `
}

prototype(Neos.NodeTypes.AssetList:AssetList.LinkItem) < prototype(Neos.Fusion:Component) {
    @context.asset = ${this.asset}
    assetLink = Neos.Fusion:ResourceUri {
        resource = ${asset.resource}
    }

    renderer = afx`
        <a href={props.assetLink} class="list-group-item list-group-item-action">
            {props.asset.title ? props.asset.title : props.asset.resource.filename}
        </a>
    `
}
