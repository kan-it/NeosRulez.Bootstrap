prototype(Neos.Neos:Page) {
    htmlTag = Neos.Fusion:Tag {
        tagName = 'html'
        omitClosingTag = TRUE
        attributes {
            lang.@if.languageDimensionExists = ${true}
            lang.@if.onlyRenderWhenInLiveWorkspace = ${true}
        }
    }
    head {
        NeosRulezBootstrapMetadata = Neos.Fusion:Component {
            renderer = afx`
                <meta name="viewport" content="width=device-width, initial-scale=1" />
                <Neos.Fusion:Tag tagName="link" attributes.rel="stylesheet" @children="attributes.href" >
                    <Neos.Fusion:ResourceUri path={'resource://NeosRulez.Bootstrap/Public/Assets/fontawesome-free-' + Configuration.setting('NeosRulez.Bootstrap.fontawesome.version') + '-web/css/all.css'} />
                </Neos.Fusion:Tag>
            `
        }
        NeosRulezBootstrapBackendCss = Neos.Fusion:Tag {
            @position = 'end'
            @process.1 = ${node.context.workspaceName != 'live' ? value : ''}
            tagName = 'link'
            attributes {
                media = 'all'
                rel = 'stylesheet'
                href = Neos.Fusion:ResourceUri {
                    path = 'resource://NeosRulez.Bootstrap/Public/Styles/Backend/backend.css'
                }
            }
        }
    }
    NeosRulezBootstrapTopAnchorLink = NeosRulez.Bootstrap:Component.TopAnchorLink
    NeosRulezBootstrapBackendJs = Neos.Fusion:Tag {
        @position = 'end'
        @process.1 = ${node.context.workspaceName != 'live' ? value : ''}
        tagName = 'script'
        attributes {
            rel = 'text/javascript'
            src = Neos.Fusion:ResourceUri {
                path = 'resource://NeosRulez.Bootstrap/Public/JavaScript/Backend/backend.js'
            }
        }
    }
    NeosRulezBootstrapAutoModal = Neos.Fusion:Component {
        @position = 'end'
        modals = ${q(documentNode).find('[instanceof NeosRulez.Bootstrap:Content.Modal]').get()}
        keyboard = 'true'

        renderer = afx`
            <Neos.Fusion:Loop items={props.modals} itemKey="modalKey">
                <script @if.render={item.properties.autoOpen} >{"
                    if(document.getElementById('modal__" + item.identifier + "')) {
                        var modal" + modalKey + " = new bootstrap.Modal(document.getElementById('modal__" + item.identifier + "'), {
                            keyboard: " + props.keyboard + "
                        });
                        var modalTimeOut" + modalKey + " = setTimeout(function(){ modal" + modalKey + ".toggle(); }, " + (item.properties.autoOpenTime ? String.toInteger(item.properties.autoOpenTime) : 0) + ");
                    }
                "}</script>
            </Neos.Fusion:Loop>
        `
    }
}
