prototype(NeosRulez.Bootstrap:Component.Navbar) < prototype(Neos.Fusion:Component) {
    headerClass = ${'neosrulez-navigation' + (Configuration.setting('NeosRulez.Bootstrap.clickableParent') ? ' navbar-cp' : '') + (Configuration.setting('NeosRulez.Bootstrap.navbarSticky') ? ' ' + 'sticky-top' : '' ) + (Configuration.setting('NeosRulez.Bootstrap.navbarHover') ? ' ' + 'navbar-hover' : '' ) + (Configuration.setting('NeosRulez.Bootstrap.navbarBackground') ? ' ' + Configuration.setting('NeosRulez.Bootstrap.navbarBackground') : '' )}
    navbarFluid = ${Configuration.setting('NeosRulez.Bootstrap.navbarFluid')}
    navbarSticky = ${Configuration.setting('NeosRulez.Bootstrap.navbarSticky')}
    navbarBreakpoint = ${Configuration.setting('NeosRulez.Bootstrap.navbarBreakpoint')}
    navbarBackground = ${Configuration.setting('NeosRulez.Bootstrap.navbarBackground')}
    navbarHover = ${Configuration.setting('NeosRulez.Bootstrap.navbarHover')}
    clickableParent = ${Configuration.setting('NeosRulez.Bootstrap.clickableParent')}
    navbarAlignment = ${Configuration.setting('NeosRulez.Bootstrap.navbarAlignment')}
    navClass = ${(this.navbarFluid ? 'container-fluid' : 'container') + ' navbar' + (this.navbarBackground == 'bg-dark' || this.navbarBackground == 'bg-primary' || this.navbarBackground == 'bg-danger' ? ' navbar-dark' : ' navbar-light') + ' navbar-expand-lg'}

    renderer = afx`
        <header class={props.headerClass} >
            <nav class={props.navClass} >
                <NeosRulez.Bootstrap:Component.Navbar.Alignment />
            </nav>
        </header>
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Alignment) < prototype(Neos.Fusion:Component) {
    navbarAlignment = ${Configuration.setting('NeosRulez.Bootstrap.navbarAlignment')}

    right = Neos.Fusion:Component {
        renderer = afx`
            <NeosRulez.Bootstrap:Component.Navbar.Brand />
            <NeosRulez.Bootstrap:Component.Navbar.Toggle />
            <NeosRulez.Bootstrap:Component.Navbar.Navbar />
        `
    }
    left = Neos.Fusion:Component {
        renderer = afx`
            <NeosRulez.Bootstrap:Component.Navbar.Navbar />
            <NeosRulez.Bootstrap:Component.Navbar.Toggle />
            <NeosRulez.Bootstrap:Component.Navbar.Brand />
        `
    }
    center = Neos.Fusion:Component {
        renderer = afx`
            <NeosRulez.Bootstrap:Component.Navbar.Navbar.Centered />
        `
    }

    navbarRenderer = ${this.navbarAlignment == 'right' ? this.right : this.navbarAlignment == 'left' ? this.left : this.navbarAlignment == 'center' ? this.center : this.right}

    renderer = afx`
        {props.navbarRenderer}
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Brand.BrandImage) < prototype(Neos.Fusion:Component) {
    @context.headerLogoPath = ${Configuration.setting('NeosRulez.Bootstrap.headerLogoPath')}
    @context.headerLogoPackage = ${Configuration.setting('NeosRulez.Bootstrap.headerLogoPackage')}
    headerLogoAltText = ${Configuration.setting('NeosRulez.Bootstrap.headerLogoAltText')}
    navbarBrand = Neos.Fusion:ResourceUri {
        path = ${'resource://' + headerLogoPackage + '/Public/' + headerLogoPath}
    }

    renderer = afx`
        <Neos.Fusion:Tag tagName="img" attributes.src={props.navbarBrand} attributes.alt={props.headerLogoAltText} />
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Brand) < prototype(Neos.Fusion:Component) {
    siteNode = ${site}
    navbarAlignment = ${Configuration.setting('NeosRulez.Bootstrap.navbarAlignment')}
    navbarBrandClass = ${'navbar-brand' + (this.navbarAlignment == 'center' ? ' mr-0' : false) + (this.navbarAlignment == 'left' ? ' mr-0 ml-3' : false)}

    renderer = afx`
        <Neos.Neos:NodeLink node={props.siteNode} attributes.class={props.navbarBrandClass} >
            <NeosRulez.Bootstrap:Component.Navbar.Brand.BrandImage />
        </Neos.Neos:NodeLink>
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Toggle) < prototype(Neos.Fusion:Component) {

    renderer = afx`
        <button class="navbar-toggler" type="button" data-toggle="collapse" data-target="#navbarSupportedContent" aria-controls="navbarSupportedContent" aria-expanded="false" aria-label="Toggle navigation"><span class="navbar-toggler-icon"></span></button>
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Navbar) < prototype(Neos.Fusion:Component) {
    menuItems = Neos.Neos:MenuItems

    renderer = afx`
        <NeosRulez.Bootstrap:Component.Navbar.Navbar.List menuItems={props.menuItems} />
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Navbar.List) < prototype(Neos.Fusion:Component) {
    listTag = 'ul'
    navbarAlignment = ${Configuration.setting('NeosRulez.Bootstrap.navbarAlignment')}
    listClass = ${'navbar-nav ' + (this.navbarAlignment == 'right' ? 'ml-auto' : false) + (this.navbarAlignment == 'left' ? 'mr-auto' : false)}
    itemTag = 'li'
    itemClass = 'nav-item'
    linkClass = 'nav-link'
    items = ${this.menuItems ? this.menuItems : false}
    itemcount = ${Array.length(this.items)/2}

    renderer = afx`
        <div class="collapse navbar-collapse" id="navbarSupportedContent">
            <Neos.Fusion:Tag @if.render={props.items} tagName={props.listTag} attributes.class={props.listClass} >
                <Neos.Fusion:Loop items={props.items} itemName="item" itemKey="itemKey" >
                    <Neos.Fusion:Tag tagName={props.itemTag} attributes.class={item.subItems ? props.itemClass + ' dropdown' : props.itemClass} >
                        <NeosRulez.Bootstrap:Component.Navbar.Navbar.List.LinkItem linkClass={item.subItems ? props.linkClass + ' dropdown-toggle' : props.linkClass} subList={item.subItems ? true : false} />
                        <NeosRulez.Bootstrap:Component.Navbar.Navbar.SubList @if.render={item.subItems} menuItems={item.subItems} />
                    </Neos.Fusion:Tag>
                </Neos.Fusion:Loop>
            </Neos.Fusion:Tag>
        </div>
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Navbar.SubList) < prototype(Neos.Fusion:Component) {
    listTag = 'div'
    listClass = 'dropdown-menu'
    linkClass = 'dropdown-item'
    items = ${this.menuItems ? this.menuItems : false}

    renderer = afx`
        <Neos.Fusion:Tag @if.render={props.items} tagName={props.listTag} attributes.class={props.listClass} attributes.aria-labelledby="navbarDropdown" >
            <Neos.Fusion:Loop items={props.items} itemName="item" itemKey="itemKey" >
                <NeosRulez.Bootstrap:Component.Navbar.Navbar.List.LinkItem linkClass={props.linkClass} />
            </Neos.Fusion:Loop>
        </Neos.Fusion:Tag>
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Navbar.Centered) < prototype(Neos.Fusion:Component) {
    itemTag = 'li'
    itemClass = 'nav-item'
    linkClass = 'nav-link'
    items = ${this.menuItems ? this.menuItems : false}
    itemcount = ${Array.length(this.items)/2}
    menuItems = Neos.Neos:MenuItems

    renderer = afx`
        <ul class="navbar-nav navbar-nav-left d-none d-lg-flex flex-fill w-100 flex-nowrap">
            <Neos.Fusion:Loop items={props.items} itemName="item" itemKey="itemKey" >
                <Neos.Fusion:Tag tagName={props.itemTag} attributes.class={item.subItems ? props.itemClass + ' dropdown' : props.itemClass} >
                    <NeosRulez.Bootstrap:Component.Navbar.Navbar.List.LinkItem @if.render={itemKey < props.itemcount ? true : false} linkClass={item.subItems ? props.linkClass + ' dropdown-toggle' : props.linkClass} subList={item.subItems ? true : false} />
                    <NeosRulez.Bootstrap:Component.Navbar.Navbar.SubList items={item.subItems} />
                </Neos.Fusion:Tag>
            </Neos.Fusion:Loop>
        </ul>
        <ul class="navbar-nav mx-auto flex-fill">
            <NeosRulez.Bootstrap:Component.Navbar.Brand />
        </ul>
        <ul class="navbar-nav navbar-nav-right d-none d-lg-flex flex-fill w-100 flex-nowrap justify-content-end">
            <Neos.Fusion:Loop items={props.items} itemName="item" itemKey="itemKey" >
                <Neos.Fusion:Tag tagName={props.itemTag} attributes.class={item.subItems ? props.itemClass + ' dropdown' : props.itemClass} >
                    <NeosRulez.Bootstrap:Component.Navbar.Navbar.List.LinkItem @if.render={itemKey >= props.itemcount ? true : false} linkClass={item.subItems ? props.linkClass + ' dropdown-toggle' : props.linkClass} subList={item.subItems ? true : false} />
                    <NeosRulez.Bootstrap:Component.Navbar.Navbar.SubList items={item.subItems} />
                </Neos.Fusion:Tag>
            </Neos.Fusion:Loop>
        </ul>
        <NeosRulez.Bootstrap:Component.Navbar.Toggle />
        <div class="d-md-block d-lg-none w-100">
            <NeosRulez.Bootstrap:Component.Navbar.Navbar.List menuItems={props.menuItems} listClass="navbar-nav" />
        </div>
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Navbar.List.LinkItem) < prototype(Neos.Fusion:Component) {
    linkItemClass = ${this.linkClass + ' ' + item.state}
    attrRole = ${this.subList ? 'button' : false}
    attrDatatoggle = ${this.subList ? 'dropdown' : false}
    attrAriahaspopup = ${this.subList ? 'true' : false}
    attrExpanded = ${this.subList ? 'false' : false}

    renderer = afx`
        <Neos.Neos:NodeLink node={item.node} attributes.role={props.attrRole} attributes.data-toggle={props.attrDatatoggle} attributes.aria-haspopup={props.attrAriahaspopup} attributes.aria-expanded={props.attrExpanded} attributes.class={props.linkItemClass} >
            <NeosRulez.Bootstrap:Component.Navbar.Navbar.List.LinkItem.FontAwesomeIcon @if.render={item.node.properties.faicon} icon={item.node.properties.faicon} onlyicon={item.node.properties.onlyicon} />
            {item.node.properties.onlyicon ? false : item.node.properties.title}
        </Neos.Neos:NodeLink>
    `
}

prototype(NeosRulez.Bootstrap:Component.Navbar.Navbar.List.LinkItem.FontAwesomeIcon) < prototype(Neos.Fusion:Component) {
    icon = ${this.icon ? this.icon : false}

    renderer = afx`
        <i @if.render={props.icon} class={props.icon + (props.onlyicon ? '' : ' mr-1')} ></i>
    `
}