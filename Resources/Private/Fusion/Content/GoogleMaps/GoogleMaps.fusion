prototype(NeosRulez.Bootstrap:Content.GoogleMaps) < prototype(Neos.Neos:ContentComponent) {
    location = NeosRulez.Bootstrap:Class.GoogleMaps {
        address = ${node.properties.address}
        zip = ${node.properties.zip}
        city = ${node.properties.city}
        country = ${node.properties.country}
        source = ${node.properties.source}
    }

    gMapsApiKey = ${Configuration.setting('NeosRulez.Bootstrap.googleMapsApiKey')}

    src = ${this.location && !node.properties.source ? 'https://www.google.com/maps/embed/v1/place?key=' + this.gMapsApiKey + '&q=' +this.location : false}
    embedSrc = ${this.location && node.properties.source ? this.location : false}

    renderer = afx`
        <NeosRulez.Bootstrap:Component.AbstractContent>
            <code @if.render={!props.gMapsApiKey} >
                {I18n.translate('NeosRulez.Bootstrap:NodeTypes.Content.GoogleMaps:content.noApiKey')}
            </code>
            <div @if.render={props.src} class="ratio ratio-16x9">
                <iframe src={props.src} allowfullscreen></iframe>
            </div>
            <div @if.render={props.embedSrc} class="ratio ratio-16x9">
                <iframe class="google-map" src={props.embedSrc} allowfullscreen></iframe>
            </div>
            <code @if.render={!props.src && !props.embedSrc} >
                {I18n.translate('NeosRulez.Bootstrap:NodeTypes.Content.GoogleMaps:content.noProperties')}
            </code>
        </NeosRulez.Bootstrap:Component.AbstractContent>
    `
}
