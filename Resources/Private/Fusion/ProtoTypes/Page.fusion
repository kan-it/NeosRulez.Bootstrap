prototype(Neos.Neos:Page) {

    NeosRulezBootstrapTheme = NeosRulez.Bootstrap:Theme

    htmlTag = Neos.Fusion:Tag {
        tagName = 'html'
        omitClosingTag = TRUE
        attributes {
            lang.@if.languageDimensionExists = ${true}
            lang.@if.onlyRenderWhenInLiveWorkspace = ${true}
        }
    }

    head {

        NeosRulezBootstrapMetaData = Neos.Fusion:Renderer {
            @position = 'before stylesheets'
            type = 'Neos.Fusion:Value'
            element.value = '<meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">'
        }

        NeosRulezBootstrapFontAwesome = Neos.Fusion:Tag {
            @position = 'before stylesheets'
            tagName = 'link'
            attributes {
                media = 'all'
                rel = 'stylesheet'
                href = Neos.Fusion:ResourceUri {
                    path = 'resource://NeosRulez.Bootstrap/Public/Assets/fontawesome-free-5.11.2-web/css/all.min.css'
                }
            }
        }

        NeosRulezBootstrapCss = NeosRulez.ScssParser:ScssFile
        NeosRulezBootstrapCss {
            @position = 'before stylesheets'
            source = 'resource://NeosRulez.Bootstrap/Private/Styles/styles.scss'
            inline = FALSE
            format = 'compressed'
            outputFolder = 'resource://NeosRulez.Bootstrap/Public/Styles/'
        }

        NeosRulezBootstrapBackendCss = Neos.Fusion:Tag {
            @position = 'before stylesheets'
            @process.1 = ${node.context.workspaceName != 'live' ? value : ''}
            tagName = 'link'
            attributes {
                media = 'all'
                rel = 'stylesheet'
                href = Neos.Fusion:ResourceUri {
                    path = 'resource://NeosRulez.Bootstrap/Public/Styles/Backend/backend.css'
                }
            }
        }

        DatedropperJs = NeosRulez.JsMinifier:JsFile
        DatedropperJs {
            @position = 'after NeosRulezBootstrapCss'
            source = 'resource://NeosRulez.Bootstrap/Private/JavaScript/datedropper.pro.min.js'
            inline = FALSE
            outputFolder = 'resource://NeosRulez.Bootstrap/Public/JavaScript/'
        }
        DatedropperJs.@if.contentIsPresent = ${q(documentNode).children('[instanceof Neos.Neos:ContentCollection]').find('[instanceof Neos.Form.Builder:NodeBasedForm]').is()}

    }

    NeosRulezBootstrapScrollToTop = Neos.Fusion:Renderer {
        @position = 'end'
        type = 'Neos.Fusion:Value'
        element.value = '<div class="topLink"><i class="fa fa-angle-up" aria-hidden="true"></i></div>'
    }

    NeosRulezBootstrapJs = NeosRulez.JsMinifier:JsFile
    NeosRulezBootstrapJs {
        @position = 'end'
        source = 'resource://NeosRulez.Bootstrap/Private/JavaScript/scripts.js'
        inline = FALSE
        outputFolder = 'resource://NeosRulez.Bootstrap/Public/JavaScript/'
    }

    #head.@process.compression >
    #body.@process.compression >

}

prototype(NeosRulez.Bootstrap:Theme) {
    @class = 'NeosRulez\\Bootstrap\\Fusion\\ThemeImplementation'
}